#!/usr/bin/perl

# use lib qw(/home/rork/Scripts/lib);
use LWP::Simple;
use File::Copy;
use File::Basename;
use Getopt::Long;
use HTML::LinkExtor;
use URI::Escape;
use Cwd qw(abs_path);
use uIni;
use upkg;
use strict;
use warnings;

# CHANGELOG
# v0.1
#   - basic script
# v0.2
#   - Overwrite the ini on update (requires latest uIni.pm)
#   - Replace utdep.pl by upkg.pm
#   - Case insensitive search on repair
#   - Add --fix-mismatch
# v0.4
#   - Fixed case in downloading files
#   - Fixes in getPath()
#   - Improve case insensitive file searching
#   - Easier version of repair (only searches the indicated directory now)
#   - Fixed bug in saving uz-get.new_ini
#   - Added multiple direction support
#   - Changed behaviour of makePath (returns path within utdir)
#   - Backup mismatched files and restore if available
# v0.5
#   - Added support for url-encoding
#   - Check for http errors when downloading files or getting repository lists
#   - Use uIni in search and install rather then just open the file
# v0.6
#   - Fixed position of repositories after update
#   - Use uz-get.ini for settings, should be in the default system dirs or current directory
#   - Set working dir to default systemdir (this may or may not fix an issue that may or may not exist on Windows systems)
#   - Made directory to store configurable, storeId points towards the directory
#   - Included a cachecleaner, can't determine filetypes

# set location of uz-get.ini, if left undefined will search for it in current directory or default UT System directories
my $pathToUzGetIni;

if (!defined($pathToUzGetIni)) {
  if (-e $ENV{'HOME'} . "/.loki/ut/System/uz-get.ini") {
    $pathToUzGetIni = $ENV{'HOME'} . "/.loki/ut/System/uz-get.ini";
  }
  elsif (-e dirname(abs_path($0)) . "/uz-get.ini") {
    $pathToUzGetIni = dirname(abs_path($0)) . "/uz-get.ini";
  }
  elsif (-e "C:\\UnrealTournament\\System\\uz-get.ini") {
    $pathToUzGetIni = "C:\\UnrealTournament\\System\\uz-get.ini";
  }
  else {
    print "uz-get.ini not found, please specify the path in the script\n";
    exit;
  }
}

# load ini and settings
my $ini = uIni->new();
   $ini->load($pathToUzGetIni);

my @utdirs;
foreach (0 .. $ini->getArrayLength("uz-get.config", "utdirs")-1) {
  my $dir = $ini->getValue("uz-get.config", "utdirs", $_);
  # check if it is a dir
  if (!-d $dir) {
    print "$dir is not a directory\n";
    next;
  }
  if ($dir !~ m/\/$/) {
    $dir .= "/";
  }
  push(@utdirs, $dir);
}

# checks for existence
if (!-d $utdirs[0] . "System") {
  print  $utdirs[0] . "System doesn't exist, please check your configuration\nOn Windows utdirs[0] should be the full path the main directory\nOn linux utdirs[0] should be the full path to ~/.loki/ut/\n";
  exit;
}

if (!-d $utdirs[0] . "Cache") {
  print $utdirs[0] . "Cache doesn't exist, created\n";
  mkdir($utdirs[0] . "Cache");
}

my $storeId = $ini->getValue("uz-get.config", "storeId");
if ($storeId > $ini->getArrayLength("uz-get.config", "utdirs")-1) {
  print "Directory for storage not found, set to default\n";
  $storeId = 0;
}
my $pathToUcc = $ini->getValue("uz-get.config", "pathToUcc");
my $pathToLog = $ini->getValue("uz-get.config", "pathToLog");



# fixed variables
my $version = '0.6';

GetOptions("d|download=s" => \&download, "h|help" => \&help, "r|repair=s" => \&repair, "u|update" => \&update, "s|search=s" => \&search, "i|install=s" => \&install, "f|fix-mismatch" => \&fixMismatch, "c|clear-cache" => \&clearCache);

sub clearCache {
 my $pathToCacheini = $utdirs[0] . "Cache/cache.ini";
 if (!-e $pathToCacheini) {
   print "cache.ini not found, make sure utdirs[0] is specified correctly\n";
   return;
 }
 
 my ($ok, $fail) = (0, 0);
 my $cini = uIni->new();
    $cini->load($pathToCacheini);
 
 if (!defined($cini->{ini}->{'Cache'})) {
   print "No files were found\n";
   return;
 }
 
 my $new_cini = uIni->new();
 my %files = %{$cini->{ini}->{'Cache'}};
 
 foreach my $cachename (keys %files) {
   my $realname = $files{$cachename};
   
   if ($realname =~ m/\.(u[mxnratz]*$)/i) {
     my $cachepath = $utdirs[0] . "Cache/" . $cachename . ".uxx";
     my $realpath = getPath($realname);
     if (!defined($realpath)) {
       $realpath = $utdirs[$storeId] . makePath($realname);
     }
     move($cachepath, $realpath);
     print "$realname --> $realpath [OK]\n";
     $ok++;
   }
   else {
     print "$realname --> unknown filetye [SKIP]\n";
     $new_cini->setValue("Cache", $cachename, $realname);
     $fail++;
   }
 }
 
 if (!$new_cini->isChanged()) {
   # make an empty header
   $new_cini->{'ini'}->{'Cache'} = {};
   $new_cini->{'horder'} = ['Cache'];
 }
 $new_cini->save($pathToCacheini);
 print "\nMoved $ok/" . ($ok + $fail) . " files\n";    
}

sub download {
  # the most important part of the script, downloads, decompresses and moves the file.
  my $self = shift;
  my $file = shift;
  my $filename = $file;
     $filename =~ s/^.*\///;
     $filename = uri_unescape($filename);

  # create the cachename and the packagename
  my $storename = $utdirs[0] . makePath($filename);
     $filename =~ s/\.uz$//i;
     
  my $packagename = getPath($filename);
    
  if (defined($packagename)) {
    print "Package $filename allready exists\n";
    return;
  }

  # download and store the file

  unless (-e $storename) {
    print "Downloading $file\n";
    my $response = getstore($file, $storename);
    
    if (!is_success($response)) {
      print "Couldn't download $file: $response\n";
      return;
    }
  }
  else {
    print "Get $filename from Cache\n";
  }
  
  if ($storename =~ m/\.uz$/) {
    print "Decompress $storename\n";
    $storename =~ s/\(/\\\(/g;
    $storename =~ s/\)/\\\)/g;
    
    # change current working directory to default System, this probably saves the unpacked file in the correct System dir.
    chdir($utdirs[0] . "System/");
    system($pathToUcc . " decompress " . $storename);
  }
  
  unless (-e $utdirs[0] . "System/" . $filename) {
    opendir(DIR, $utdirs[0] . "System/");
    foreach (readdir(DIR)) {
      if (lc($_) eq lc($filename)) {
        $filename = $_;
      }
    }
  }
  
  $packagename = $utdirs[$storeId] . makePath($filename);
  
  print "Move to " . $packagename . "\n";
  move($utdirs[0] . "/System/" . $filename ,$packagename) or die "Can't move package (" . $utdirs[0] . "/.loki/ut/System/" . $filename . ": $!";
}

sub install {
  my $self = shift;
  my $query = shift;
     $query = uri_escape($query);

  # first find available url's
  my @urls;
     
  foreach (0 .. $ini->getArrayLength("uz-get.files", "file")-1) {
    my $url = $ini->getValue("uz-get.files", "file", $_);
    if ($url =~ m/\/$query\.uz$/i) {
      push(@urls, $url);
    }
  }
  if ($#urls == -1) {
    print "File ($query) not found\n";
    return;
  }

  # choose a random url, should build a decent loadbalancer
  my $rand = srand($#urls);
  # print "Get url $rand\n";

  # download file and dependencies
  download(undef, $urls[$rand-1]);
  $query = uri_unescape($query);
  repair(undef, $query);
}

sub help {
  print <<HelpText;
uz-get v$version
search and download ut files from redirectservers

Usage: uz-get -dhirsuf ?file?

Options:
   -d   --download URL      Downloads and install a networkpackage
   -h   --help              This help text
   -i   --install  PACKAGE  Searches and downloads a package
   -r   --repair   PACKAGE  Checks for and installs missing dependencies
   -s   --search   PACKAGE  Search for the package in the repository
   -u   --update            Update the filelist
   -f   --fix-mismatch      Check the UnrealTournament.log for version mismatches and missing files
   -c   --clear-cache       Clear the cache, move files to their respective directories

HelpText
}

sub search {
  my $self = shift;
  my $query = shift;
     $query = uri_escape($query);
  my %results;
    
  foreach (0 .. $ini->getArrayLength("uz-get.files", "file")-1) {
    my $url = $ini->getValue("uz-get.files", "file", $_);
    if ($url =~ m/\/[^\/]*$query[^\/]*$/i) {
      $url =~ s/[\r\n]//g;
      $url =~ s/.*\///;
      $url =~ s/\.uz$//i;
      $results{uri_unescape($url)}++;
    }
  }

  print join("\n", sort keys %results);
  print "\n\n";
}

sub repair {
  my $self = shift;
  my $pkg = shift;

  #$pkg =~ s/\(/\\\(/g;
  #$pkg =~ s/\)/\\\)/g;

  # get the filetype
     $pkg =~ m/\.(u[mxnrat]*)/i;
  my $type = lc($1);

  # check if the file exists
  my $pkgPath = getPath($pkg);
  unless (defined($pkgPath)) {
    print "Can't repair $pkg: file not found\n";
    return;
  }

  # get the dependencies
  my $opkg = upkg->new();
     $opkg->load($pkgPath);
     
  my @deps = $opkg->getDependencies();
  
  foreach my $dependency (@deps) {
    if ($dependency =~ m/\.uxx$/i) {
      print "Ignore $dependency: extension not found\n";
      next;
    }
    my $path = getPath($dependency);
    if (!defined($path)) {
      install(undef, $dependency);
    }
  }
}

sub update {
  my $new_ini = uIni->new();
  my $i = 0;
  my $numFiles = 0;
  
  # store settings in new ini
  for (my $i = 0; $i <= $#utdirs; $i++) {
    $new_ini->setValue("uz-get.config", "utdirs[$i]", $utdirs[$i]);
  }
  
  $new_ini->setValue("uz-get.config", "pathToUcc", $pathToUcc);
  $new_ini->setValue("uz-get.config", "pathToLog", $pathToLog);
  $new_ini->setValue("uz-get.config", "storeId", $storeId);
  
  # retrieve each repository and update files
  while(1) {
    my @files;
    my $repository = $ini->getValue("uz-get.files", "Repository", $i);
    unless (defined($repository)) {
      last;
    }
    $new_ini->setValue("uz-get.files", "Repository[$i]", $repository);
    print "Get $repository\n";

    # fetch the page 
    my $page =  get($repository);
    
    if (!defined($page)) {
      print "Couldn't get $repository\n";
    }
    else {
      my $parser = new HTML::LinkExtor (sub {my ($tag, %links) = @_; push(@files, values(%links))});
	$parser->parse ($page);
	$parser->eof;
      foreach my $file(@files) {
	next unless ($file =~ m/.uz$/i);
	if ($file !~ m/^http/i) {
	  $file =~ s/^\///;
	  
	  # unescape to prevent double escaping
	  $file = uri_unescape($file);
	  $file = $repository . uri_escape($file);
	}
	# print "Found: $file\n";
	$new_ini->setValue("uz-get.files", "file[$numFiles]", $file);
	$numFiles++;
      }
    }
    $new_ini->setPosition("uz-get.files", "Repository[$i]", $i);
    $i++;
  }
  $new_ini->save($pathToUzGetIni);
}

sub fixMismatch {
  my %files;
  my $redirect;
  my $compression = 0;
  
  open(LOG, "<", $pathToLog) or die "Can't open log ($pathToLog): $!\n";
  while(<LOG>) {
    chomp;
    my @line = split(/\s+/);
    
    # detect new connection:
    # Log: Browse: 176.57.143.206/Index.unr?Name=Rork?Class=BotPack.TMale1?team=0?skin=CommandoSkins.daco?Face=CommandoSkins.Boris?Voice=BotPack.VoiceMaleOne?OverrideClass=
    if ($line[0] eq "Log:" and $line[1] eq "Browse:") {
      # reconnect, reset the files
      %files = ();
    }
    
    # detect packages
    # DevNet: PendingLevel received: USES GUID=B9D3048E4988166ADE95A99FF86E339E PKG=MH-ATAA0-(Obnoxious)+fix2 FLAGS=1 SIZE=1001070 GEN=1 FNAME=MH-ATAA0-(Obnoxious)+fix2.unr
    if ($line[0] eq "DevNet:" and $line[1] eq "PendingLevel" and $line[3] eq "USES") {
      my $guid = substr($line[4], 5);
      my $file = substr($line[9], 6);
         $file =~ s/^"//;
         $file =~ s/"$//;
      $files{$file} = $guid;
    }
    
    # detect redirect
    # DevNet: PendingLevel received: DLMGR CLASS=IpDrv.HTTPDownload PARAMS=http://176.57.143.206/redirect/ COMPRESSION=1
    if ($line[0] eq "DevNet:" and $line[1] eq "PendingLevel" and $line[3] eq "DLMGR" and $line[4] eq "CLASS=IpDrv.HTTPDownload") {
      $redirect = substr($line[5], 7);
      $compression = substr($line[6], -1);
    }
  }
  close(LOG);
  
  if (!defined($redirect)) {
    print "No redirect found, can't do anything\n";
    exit;
  }
  
  if ($redirect !~ m/\/$/) {
    $redirect .= "/";
  }
  print "Redirect: $redirect\n";
  print "Compression: $compression\n";
  print "Files: " . (scalar(keys  %files) + 1) . "\n\n";
  
  foreach my $file (sort {lc($a) cmp lc($b)} keys %files) {
    
    print $file;
    
    
    my $guid = $files{$file};

    my $path = getPath($file);
    my $link = $redirect . $file;
    if ($compression) {
      $link .= ".uz";
    }
    if (!defined($path)) {
      print " [DOWNLOAD]\n";

      download(undef, $link);
    }
    else {
      my $pkg = upkg->new;
         $pkg->load($path);
         
      my $curguid = $pkg->{'headers'}->{'GUID'};
      my $version = $pkg->{'headers'}->{'Version'};
      
      if ($version < 68) {
        print " [NO GUID]\n";
      }
      else {
        $curguid =~ s/-//g;
        $curguid = uc($curguid);
                
        if ($guid eq $curguid) {
          print " [OK]\n";
        }
        else {
          print " [REPLACE]\n";
          my $backup = $utdirs[0] . "Cache/" . $file . "-" . $curguid;
          move($path, $backup);
          
          print "old: " . $curguid . "\n";
          print "new: " . $guid . "\n";

          my $cache = $utdirs[0] . "Cache/" . $file . "-" . $guid;
          if (-e $cache) {
            print "Move $file Cache\n";
            move($cache, $path);
          }
          else {            
            my $cache = getPath($file . ".uz");
            if (defined($cache)) {
              print "Remove $file.uz from Cache\n";
              unlink($cache);
            }
          
            my $link = $redirect . $file;
            if ($compression) {
              $link .= ".uz";
            }
            download(undef, $link);
          }
        }
      }
    }
  }
}

sub getPath {
  # case insensitive search for paths of existing files
  my $file = shift;
  
  my $dir = dirname($file);
  my $filename = basename($file);
  
  my $pkgPath;
    
  if ($dir ne ".") {
    $pkgPath = $file;
  }
  else {
    $pkgPath = makePath($file);
  }
  
  foreach my $utdir (@utdirs) {
    # check if the file exists
    unless (-e $utdir . $pkgPath) {
      # no, case insensitive search
      my $dir = dirname($utdir . $pkgPath);
      opendir(DIR, $dir);
      foreach (readdir(DIR)) {
	if (lc($_) eq lc($filename)) {
	  $pkgPath = $dir . "/" . $_;
	}
      }
    }
    else {
      $pkgPath = $utdir . $pkgPath;
    }
    #check again
    if (-e $pkgPath) {
      return $pkgPath;
    }
  }
  return undef;
}

sub makePath {
  # make the expected path of a file
  my %type2dir = (
  "u" => "System/",
  "unr" => "Maps/",
  "umx" => "Music/",
  "utx" => "Textures/",
  "uax" => "Sounds/",
  "uz" => "Cache/");
  
  my $file = shift;
     $file =~ m/\.(u[mxnratz]*$)/i;
     
  my $type = lc($1);
  
  my $pkgPath = $type2dir{$type} . $file;
  
  return $pkgPath;
}
